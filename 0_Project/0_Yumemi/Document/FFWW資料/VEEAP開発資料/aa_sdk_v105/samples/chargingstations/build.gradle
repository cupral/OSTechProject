/**
 * The first line in the build configuration applies the Android plugin for
 * Gradle to this build and makes the android {} block available to specify
 * Android-specific build options.
 */

apply plugin: 'com.android.application'

/**
 * The android {} block is where you configure all your Android-specific
 * build options.
 */

android {

  /**
   * compileSdkVersion specifies the Android API level Gradle should use to
   * compile your app. This means your app can use the API features included in
   * this API level and lower.
   *
   * buildToolsVersion specifies the version of the SDK build tools, command-line
   * utilities, and compiler that Gradle should use to build your app. You need to
   * download the build tools using the SDK Manager.
   */

  compileSdkVersion 24
  buildToolsVersion "25.0.2"

  /**
   * The defaultConfig {} block encapsulates default settings and entries for all
   * build variants, and can override some attributes in main/AndroidManifest.xml
   * dynamically from the build system. You can configure product flavors to override
   * these values for different versions of your app.
   */

  defaultConfig {

    /**
     * applicationId uniquely identifies the package for publishing.
     * However, your source code should still reference the package name
     * defined by the package attribute in the main/AndroidManifest.xml file.
     */

    applicationId 'com.google.android.apps.auto.sdk.samples.chargingstations'

    // Defines the minimum API level required to run the app.
    minSdkVersion 21

    // Specifies the API level used to test the app.
    targetSdkVersion 24

    // Defines the version number of your app.
    versionCode 10

    // Defines a user-friendly version name for your app.
    versionName "1.0"

    jackOptions {
      enabled true
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
    }
  }

  /**
   * The buildTypes {} block is where you can configure multiple build types.
   * By default, the build system defines two build types: debug and release. The
   * debug build type is not explicitly shown in the default build configuration,
   * but it includes debugging tools and is signed with the debug key. The release
   * build type applies Proguard settings and is not signed by default.
   */

  buildTypes {

    /**
     * By default, Android Studio configures the release build type to enable code
     * shrinking, using minifyEnabled, and specifies the Proguard settings file.
     */

    // Note that this will only build an unsigned APK. To build a signed APK
    // follow instructions on
    // https://developer.android.com/studio/publish/app-signing.html
    release {
        minifyEnabled true // Enables code shrinking for the release build type.
        proguardFile getDefaultProguardFile('proguard-android.txt')
        proguardFile 'proguard-rules.txt'
    }
  }
}

repositories {
  flatDir {
    dirs '../../libs'
  }
}

dependencies {
    compile(name:'gearhead-sdk', ext:'aar')
    compile(name:'gearhead-sdk-ui', ext:'aar')
    compile(name:'android-support-car', ext:'aar')

    compile 'com.android.support:appcompat-v7:24.1.0'
    compile 'com.android.support:recyclerview-v7:24.1.0'
    compile 'com.google.android.gms:play-services-location:10.0.1'
    compile 'com.google.android.gms:play-services-maps:10.0.1'
}

